<?xml version="1.0" encoding="ISO-8859-1"?>
<SourceFile><Source><![CDATA[Option Explicit
Implements IJabacoControl
Implements MouseListener 
Implements MouseMotionListener
Implements ChangeListener 
Implements FocusListener 

Event GotFocus()
Event LostFocus()
Event Change()
Event Click()
Event MouseEntered()
Event MouseExited() 
Event MouseDown(Button As Integer, Shift As Integer, X As Single, Y As Single)
Event MouseMove(Button As Integer, Shift As Integer, X As Single, Y As Single)
Event MouseUp(Button As Integer, Shift As Integer, X As Single, Y As Single)

Dim myMouseIcon As #IResource

' *** Variables for Anchor and resizing ***
Private myAnchor As AnchorStyles = AnchorStyles.vbTopLeft
Private mySavedWidth As Single
Private mySavedHeight As Single
Private mySavedLeft As Single
Private mySavedTop As Single
Private hostSavedWidth As Single
Private hostSavedHeight As Single
Private isResizeTriggerInitialized As Boolean = False
Private isLeftInitialized As Boolean = False
Private isTopInitialized As Boolean = False

Public Property Get Parent() As JProgressBar
   Parent = Base
End Property

' ******************************** DEFAULT FOCUS - EVENTS ********************************

Public Sub focusGained(arg2 As FocusEvent)
   Raiseevent GotFocus()
End Sub

Public Sub focusLost(arg2 As FocusEvent)
   Raiseevent LostFocus()
End Sub

Public Sub mouseMoved(e As MouseEvent) 
   RaiseEvent MouseMove(getVBMouseButton(e),getVBMouseShift(e),e.getX(),e.getY())
End Sub

Public Sub mouseDragged(e As MouseEvent) 
   RaiseEvent MouseMove(getVBMouseButton(e),getVBMouseShift(e),e.getX(),e.getY())
End Sub

Public Sub mousePressed(e As MouseEvent) 
   RaiseEvent MouseDown(getVBMouseButton(e),getVBMouseShift(e),e.getX(),e.getY())
End Sub

Public Sub mouseReleased(e As MouseEvent) 
   If (e.getX > 0 And e.getY > 0 And e.getX < Me.getWidth And e.getY < Me.getHeight) Then RaiseEvent Click()
   RaiseEvent MouseUp(getVBMouseButton(e),getVBMouseShift(e),e.getX(),e.getY())
End Sub

Public Sub mouseClicked(e As MouseEvent) 
   'RaiseEvent Click()
End Sub

Public Sub mouseEntered(e As MouseEvent) 
   RaiseEvent MouseEntered()
End Sub 

Public Sub mouseExited(e As MouseEvent) 
   RaiseEvent MouseExited()
End Sub

Public Sub stateChanged(e As ChangeEvent) 
   Raiseevent Change 
End Sub

Public Sub Class_Initialize()
   Call Parent.addFocusListener(Me)
   Me.setVisible False
   Base.setValue 0
   Call Me.addChangeListener(Me)
   Call Me.addMouseListener(Me)
   Call Me.addMouseMotionListener(Me)
End Sub

Public Property Get BackColor() As Long
   BackColor = ColortoRGB(Base.getBackground())
End Property

Public Property Let BackColor(v As Long)
   If v <> -1 Then ' the non-color -1
      Base.setBackground(RGBtoColor(v))
   End If
End Property

Public Property Get ForeColor() As Long
   ForeColor = ColortoRGB(Me.getForeground())
End Property

Public Property Let ForeColor(v As Long)
   If v <> -1 Then ' the non-color -1
      Me.setForeground(RGBtoColor(v))
   End If
End Property

Public Property Get Width() As Single
   Width = Me.getSize.Width
End Property

Public Property Let Width(v As Single)
   Me.setSize(v, Me.getSize.Height)
End Property

Public Property Get Height() As Single
   Height = Me.getSize.Height
End Property

Public Property Let Height(v As Single)
   Me.setSize(Me.getSize.Width, v)
End Property

Public Property Get Left() As Single
   Left = Me.getLocation.x
End Property

Public Property Let Left(v As Single)
   If isLeftInitialized = False Then
       mySavedLeft = v
       isLeftInitialized = True
   End If
   Me.setLocation(v, Me.getLocation.y)
End Property

Public Property Get Top() As Single
   Top = Me.getLocation.y
End Property

Public Property Let Top(v As Single)
   If isTopInitialized = False Then
       mySavedTop = v
       isTopInitialized = True
   End If
   Me.setLocation(Me.getLocation.x, v)
End Property

Public Property Get Anchor() AS AnchorStyles
   Anchor = myAnchor
End Property

Public Property Let Anchor(val As AnchorStyles)
   myAnchor = val
End Property

Public Function Move(Left As Single, Optional Top As Single = -1, Optional Width As Single = -1, Optional Height As Single = -1)
   Me.Left = Left
   If Top <> -1 Then Me.Top = Top
   If Width <> -1 Then Me.Width = Width
   If Height <> -1 Then Me.Height = Height
End Function

Public Property Let Orientation(v As fmOrientation)
   If v = fmOrientationVertical Then 
      Base.setOrientation Adjustable.VERTICAL
   Else
      Base.setOrientation Adjustable.HORIZONTAL
   End If
End Property

Public Property Get Orientation() As fmOrientation
   If Base.getOrientation() = Adjustable.VERTICAL Then 
      Orientation = fmOrientationVertical
   Else
      Orientation = fmOrientationHorizontal
   End If
End Property

Public Property Let Min(v As Integer)
   Base.setMinimum v
End Property

Public Property Get Min() As Integer 
   Min = Base.getMinimum()
End Property

Public Property Let Max(v As Integer)
   Base.setMaximum v
End Property

Public Property Get Max() As Integer 
   Max = Base.getMaximum()
End Property

Public Property Let Value(v As Integer)
   Base.setValue v
End Property

Public Property Get Value() As Integer 
   Value = Base.getValue()
End Property

Public Property Let PaintString(v As Boolean)
   Me.setStringPainted v
End Property

Public Property Get PaintString() As Boolean 
   PaintString = Base.isStringPainted()
End Property

Public Property Let InDeterminate(v As Boolean)
   Me.setIndeterminate v
End Property

Public Property Get InDeterminate() As Boolean 
   InDeterminate = Base.isIndeterminate()
End Property

Public Property Get ToolTip() As String 
    ToolTip = Me.getToolTipText()
End Property

Public Property Let ToolTip(v As String)
   If Len(v) = 0 Then 
      Me.setToolTipText(Null)
   Else
      Me.setToolTipText(v)
   End If
End Property

Public Property Get Enabled() As Boolean 
   Enabled = Me.isEnabled()
End Property

Public Property Let Enabled(v As Boolean)
   Call Me.setEnabled(v)
End Property

Public Property Get Visible() As Boolean 
   Visible = Me.isVisible()
End Property

Public Property Let Visible(v As Boolean)
   Me.setVisible v
End Property

Public Property Get MousePointer() As MousePointerConstants
   MousePointer = JCursorToVBMousePointer(Parent.getCursor())
End Property

Public Property Let MousePointer(v As MousePointerConstants)
   Call Parent.setCursor(VBMousePointerToJCursor(v))
End Property

Public Property Get MouseIcon() As #IResource
   MouseIcon = myMouseIcon
End Property

Public Property Let MouseIcon(v As #IResource)
   myMouseIcon = v
   Call Parent.setCursor(VBMouseIconToJCursor(v, Me))
End Property

Public Property Get CanGetFocus() As Boolean
   CanGetFocus = Parent.isFocusable
End Property

Public Property Let CanGetFocus(v As Boolean)
   Parent.setFocusable v
End Property

Public Sub SetFocus()
   Parent.requestFocus 
End Sub

Public Sub Refresh()
   Call Parent.invalidate()
   Call Parent.repaint(0, 0, 0, Me.Width, Me.Height)
End Sub

Public Sub ResizeTriggerInit(w As Single, h As Single)
   hostSavedWidth = w
   hostSavedHeight = h
   mySavedWidth = Me.Width
   mySavedHeight = Me.Height
   isResizeTriggerInitialized = True
End Sub

Public Sub ResizeTrigger(w As Single, h As Single)
   If isResizeTriggerInitialized = True And isLeftInitialized = True And isTopInitialized = True Then
      Select Case myAnchor And 12 ' horizontal re-allignment
        ' 12 in decimal = 1100 in binary. binary addition - eliminates Top and Bottom
      Case AnchorStyles.vbLeft
         ' do nothing
      Case AnchorStyles.vbRight
        Me.Left = w-hostSavedWidth+mySavedLeft
      Case AnchorStyles.vbLeftRight
        Me.Width = w-hostSavedWidth+mySavedWidth
      Case AnchorStyles.vbNone
        Me.Left = (w-mySavedWidth)/(hostSavedWidth-mySavedWidth)*mySavedLeft
      End Select

      Select Case myAnchor And 3 ' vertical re-allignment
        ' 3 in decimal = 0011 in binary. binary addition - eliminates Left and Right
      Case AnchorStyles.vbTop
         ' do nothing
      Case AnchorStyles.vbBottom
         Me.Top = h-hostSavedHeight+mySavedTop
      Case AnchorStyles.vbTopBottom
         Me.Height = h-hostSavedHeight+mySavedHeight
      Case AnchorStyles.vbNone
         Me.Top = (h-mySavedHeight)/(hostSavedHeight-mySavedHeight)*mySavedTop
      End Select
   End If
End Sub
]]></Source><Param Name="(Name)" Value="ProgressBar"/><Param Name="(SuperClass)" Value="javax/swing/JProgressBar"/><Param Name="(Type)" Value="Class"/><Param Name="(Access)" Value="Public"/><Param Name="(NameSpace)" Value="VB"/></SourceFile>
