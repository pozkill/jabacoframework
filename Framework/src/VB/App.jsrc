<?xml version="1.0" encoding="ISO-8859-1"?>
<SourceFile><Source><![CDATA[Option Explicit

Public Property Get Path() As String
   Dim tmpFile As String
   Dim tmpString As String
   
   Dim tmpFileParts() As String
   tmpFileParts = Split(System.getProperty("sun.java.command"), " ")
   tmpFile = tmpFileParts(0)
   
   If InStr(System.getProperty("sun.java.command"), System.getProperty("file.separator")) = 0 Then ' Class or Jar File allone, without full path to it
     If Len(tmpFile) > 3 And Mid(tmpFile, Len(tmpFile)-3).equalsIgnoreCase(".jar") Then ' is jar file without path
       tmpString = "."
     Else
       tmpString = New java#io#File(Class.forName(tmpFile, False, ClassLoader.getSystemClassLoader()).getProtectionDomain.getCodeSource.getLocation.toURI.getPath).getAbsolutePath
     End If
   Else ' is Jar file with full path
     tmpString = Left(System.getProperty("sun.java.command"), InStrRev(System.getProperty("sun.java.command"), System.getProperty("file.separator"))-1)  
   End If

   If Right$(tmpString, 1) = System.getProperty("file.separator") Then
     tmpString = Left$(tmpString, Len(tmpString)-1)
   End If
   
   If InStr(tmpString, System.getProperty("file.separator")) = 0 Then
     tmpString = tmpString + System.getProperty("file.separator")
   End If

   Path = tmpString
End Property

Public Property Get JabacoFrameworkPath() As String
   Dim tmpFile As java#io#File
   Dim tmpString As String
  
   tmpFile = New java#io#File(getClass.getProtectionDomain.getCodeSource.getLocation.toURI.getPath)
   If tmpFile.isFile Then ' started as Jar-file or in an zip-file
       tmpString = Left(tmpFile.getAbsolutePath, InStrRev(tmpFile.getAbsolutePath, System.getProperty("file.separator"))-1)  
   Else  ' started in a directory
       tmpString = tmpFile.getAbsolutePath
   End If

   If Right$(tmpString, 1) = System.getProperty("file.separator") Then
     tmpString = Left$(tmpString, Len(tmpString)-1)
   End If
   
   If InStr(tmpString, System.getProperty("file.separator")) = 0 Then
     tmpString = tmpString + System.getProperty("file.separator")
   End If

   JabacoFrameworkPath = tmpString
End Property

Public Property Get ExeName() As String
   Dim tmpFile As String
   Dim tmpString As String
   
   Dim tmpFileParts() As String
   tmpFileParts = Split(System.getProperty("sun.java.command"), " ")
   tmpFile = tmpFileParts(0)

   If Len(tmpFile) > 3 And Mid(tmpFile, Len(tmpFile)-3).equalsIgnoreCase(".jar") Then ' is jar file
     tmpFileParts = Split(tmpFile, System.getProperty("file.separator"))
     tmpFile = tmpFileParts(UBound(tmpFileParts))
     tmpString = tmpFile
   Else
     tmpString = tmpFile & ".class"
   End If

   ExeName = tmpString
End Property

Public Property Get StartCommand() As String
  StartCommand = System.getProperty("sun.java.command")
End Property

' Gives out the arguments directed to the JVM
Public Property Get JVMArguments() As String
   Dim tmpString = ""
   Dim rbean As java#lang#management#RuntimeMXBean
   rbean = java#lang#management#ManagementFactory.getRuntimeMXBean
   Dim argumentList As java#util#List
   argumentList = rbean.getInputArguments
   Dim i As Integer
   For i=0 To argumentList.size()-1
     If i>0 Then
       tmpString = tmpString & " "
     End If
     tmpString = tmpString & argumentList.get(i)
   Next i
   JVMArguments = tmpString
End Property
]]></Source><Param Name="(Name)" Value="App"/><Param Name="(SuperClass)" Value="java/lang/Object"/><Param Name="(NameSpace)" Value="VB"/><Param Name="(Type)" Value="Class"/><Param Name="(Access)" Value="Public"/></SourceFile>
